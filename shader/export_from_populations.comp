// --- COMP ---
#version 450 core

// input uniforms
layout( push_constant ) uniform Push_Constant {
    int ping;
    int values_per_cell;
} pc;

// uniform buffer
layout( std140, binding = 5 ) uniform Compute_UBO {
    float   omega;            // collision frequency
    float   wall_velocity;
    int     wall_thickness;
    int     comp_index;
};

// specialization constants for local work group size
layout( local_size_x_id = 0 ) in;
//layout( local_size_y_id = 1 ) in;
//layout( local_size_z_id = 2 ) in;

// local work group defaults, when constants are not defined
layout( local_size_x = 256 ) in;
layout( local_size_y =   1 ) in;
layout( local_size_z =   1 ) in;


// populations 1 single buffered rest velocity, 8 double buffered link velocities
layout( binding = 2, r32f  ) uniform restrict imageBuffer popul_buffer;

// velocity and density write target, will be rendered as texture in next pass
layout( binding = 8, r32f /*rgba16f*/ ) uniform restrict writeonly imageBuffer export_buffer[2];


// population indexes
// 6 2 5 //
//  \|/  //
// 3-0-1 //
//  /|\  //
// 7 4 8 //

/*
// Directions:          R       E       N       W       S       NE      NW      SW      SE
const float[9] pw = {   4.0/9,  1.0/9,  1.0/9,  1.0/9,  1.0/9,  1.0/36, 1.0/36, 1.0/36, 1.0/36 };
const int[9]  inv = {   0, 3, 4, 1, 2, 7, 8, 5, 6 };
*/

// Cell Type Attributes
//#define NOSLIP 1
//#define FORCE 2


////////////////////////////////////////////////////////
// Common Indexes and Counts for VkImage and VkBuffer //
////////////////////////////////////////////////////////

// define access indexes for the export buffers
#define PCPING pc.ping
#define PING ( 8 * PCPING )
#define PONG ( 8 -   PING )



//////////////////////////////////////////
// Indexes and Counts if using VkBuffer //
//////////////////////////////////////////

// We use one rest position and two times the stream populations due to double buffering
// loading populations p with variable PING = 0 V 8 and storing with pong = 8 V 0
// All populations are stored in one continuous float array/buffer
// first come N rest populations then N east, N north, ...etc.,
// With the index variable I = [0..N) we can reach individual velocities of population interval at p
// We load and store rest populations with index I
// load stream populations index ( PING + 1 ) * N + I and store with ( pong + 1 ) * N + I
// with this we reach the population of any node
// the 2D output image is accessed simply with ivec2( X, Y )

// define global equivalent for gl_LocalInvocationIndex
#define UI gl_GlobalInvocationID.x

// U and I are also the index velocity population index
#define I int( UI )

// derived total count of nodes
int cell_count = int( gl_WorkGroupSize.x * gl_NumWorkGroups.x );



// main simulation function
void main( void ) {

    // population indexes
    // 6 2 5 //
    //  \|/  //
    // 3-0-1 //
    //  /|\  //
    // 7 4 8 //

    // load populations
    float f[9] = {
        imageLoad( popul_buffer,                             I ).r,
        imageLoad( popul_buffer, ( PONG + 1 ) * cell_count + I ).r,
        imageLoad( popul_buffer, ( PONG + 2 ) * cell_count + I ).r,
        imageLoad( popul_buffer, ( PONG + 3 ) * cell_count + I ).r,
        imageLoad( popul_buffer, ( PONG + 4 ) * cell_count + I ).r,
        imageLoad( popul_buffer, ( PONG + 5 ) * cell_count + I ).r,
        imageLoad( popul_buffer, ( PONG + 6 ) * cell_count + I ).r,
        imageLoad( popul_buffer, ( PONG + 7 ) * cell_count + I ).r,
        imageLoad( popul_buffer, ( PONG + 8 ) * cell_count + I ).r
    };

    // compute macroscopic properties
    float rho = f[0] + f[1] + f[2] + f[3] + f[4] + f[5] + f[6] + f[7] + f[8];
    vec2  vel = ( vec2(f[1],f[2]) - vec2(f[3],f[4]) + vec2(f[5]) - vec2(f[7]) + vec2(f[8],f[6]) - vec2(f[6],f[8])) / rho;

    // store properties in export buffer with ensight vector format
    imageStore( export_buffer[ PCPING ],                  I, vec4( vel.x ));
    imageStore( export_buffer[ PCPING ],     cell_count + I, vec4( vel.y ));
    imageStore( export_buffer[ PCPING ], 2 * cell_count + I, vec4(     0 ));  // vel.z

}   // main

